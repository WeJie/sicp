## 1.3  Formulating Abstractions with Higher-order Procedures 

> Procedures that mainipulate procedures are called higher-order procedures.



### 1.3.1 Procedures as Arguments 过程作为参数

```lisp
(define (sum-intergers a b)
  (if (> a b)
      0
      (+ a (sum-intergers a b))))

(define (cube n)
  (* n n n))

(define (sum-cubes a b)
  (if (> a b)
      0
      (+ (cube a) (sum-cubes a b))))

(define (pi-sum a b)
  (if (> a b)
      0
      (+ (/ 1.0 (* a (+ a 2))) (pi-sum (+ a 4) b))))

; 这三个过程遵循着一样的模式
;(define (<name> a b)
;  (if (> a b)
;      0
;      (+ (<term> a)
;         (<name> (<next> a) b))))
```

可以抽象成下方的公式：
$$
\sum_{n=a}^b f(n) = f(a) + ... + f(b)
$$

```lisp
; 根据三个过程的通用模式，抽象出过程
(define (sum term a next b)
  (if (> a b)
      0
      (+ (term a)
         (sum term (next a) next b))))
```

根据通用过程 sum，上面的三个过程可以这样实现：

```lisp
(define (inc n) (+ n 1))

(define (identity x) x)

(define (sum-intergers a b)
  (sum identity a inc b))

(define (cube n)
  (* n n n))

(define (sum-cubes a b)
  (sum cube a inc b))

(define (pi-sum a b)
  (define (pi-term x)
    (/ 1.0 (* a (+ a 2))))
  (define (pi-next x)
    (+ x 4))
  (sum pi-term  a pi-next b))
```

我们可以将 sum 作为基础组件，通过下面的函数求定积分
$$
\int_a^b f \approx [f(a + \frac {dx}2) + f(a + dx + \frac{dx}2) + f(a + 2dx + \frac {dx}2) +...]dx
$$

```lisp
(define (integral f a b dx)
  (define (add-dx x) (+ x dx))
  (* (sum f (+ a (/ dx 2.0)) add-dx b) dx))

(integral cube 0 1 0.01)
; 0.24998750000000042
; > 
```



### 练习

1.29 [Simpson's Rule ](https://www.wikiwand.com/en/Simpson's_rule) 是一个比上面出现的方法更加准确的求积分方法。
$$
\int_a^b f \approx \frac h3[y_0 + 4y_1 + 2y_2 + 4y_3 + 2y_4 + ... + 2y_{n-2} + 4y_{n-1} + y_n]
$$
$h = (b - a)/n$ ，n 是某个偶数，$y_k = f(a + kh)$ （n 的值越大，定积分的值就越准确，$f$ 为被积分函数）。

设计一个过程，以 $f$，a，b 和 n 为参数，使用 Simpson's Rule 来计算积分。用设计好的过程计算 cube 在[0，1]的积分（n = 100，n = 1000），与上面的积分方法对比。

根据分析可以得到下面的函数
$$
\int_a^b f \approx \frac h3 \cdot \sum_{k=0}^n factor \cdot y_k
$$

$$
h = (b - a)/n, y_k = f(a + kh)
$$

$$
factor = \cases {
1&if k = 0, k = n \cr
2 & if k 是偶数 \cr
4 & if k 是奇数 
}
$$

```lisp
(define (cube n)
  (* n n n))

(define (sum term a next b)
  (if (> a b)
      0
      (+ (term a)
         (sum term (next a) next b))))

(define (simpson-rule f a b n)
  (define h (/ (- b a) n))

  (define (y k)
    (f (+ a (* k h))))
  
  (define (factor k)
    (cond ((or (= k 0) (= k n)) 1)
          ((even? k) 2)
          ((odd? k) 4)))

  (define (sim-term k)
    (* (y k) (factor k)))
  
  (define (sim-next k)
    (+ k 1))

  (if (odd? n)
      "n must be even!"
      (* (sum  sim-term 0.0 sim-next n) (/ h 3))))

(simpson-rule cube 0 1 100)
(simpson-rule cube 0 1 1000)

(define (integral f a b dx)
  (define (add-dx x)
    (+ x dx))
  (* (sum f (+ a (/ dx 2.0)) add-dx b) dx))

(integral cube 0 1 0.01)
(integral cube 0 1 0.001)

; 0.24999999999999992
; 0.2500000000000003
; 0.24998750000000042
; 0.249999875000001
; > 
; 结果更精确
```



1.30 上面的 sum 过程是线性递归。完成下面的表达式，改写该过程为线性迭代。

```lisp
(define (sum term a next b)
  (define (iter a result)
    (if <??>
        <??>
        (iter <??> <??>)))
  (iter <??> <??>))
```

迭代版本

```lisp
#lang racket

(define (sum term a next b)
  (if (> a b)
      0
      (+ (term a)
         (sum term (next a) next b))))

(define (sum2 term a next b)
  (define (iter a result)
    (if (> a b)
        result
        (iter (next a) (+ result (term a)))))
  (iter a 0))

(define (inc n) (+ n 1))

(define (cube n)
  (* n n n))

(define (sum-cubes a b)
  (sum cube a inc b))

(define (sum-cubes2 a b)
  (sum2 cube a inc b))

(require racket/trace)
(trace sum)
(trace sum2)

(sum-cubes 2 8)
(sum-cubes2 2 8)

; >(sum #<procedure:cube> 2 #<procedure:inc> 8)
; > (sum #<procedure:cube> 3 #<procedure:inc> 8)
; > >(sum #<procedure:cube> 4 #<procedure:inc> 8)
; > > (sum #<procedure:cube> 5 #<procedure:inc> 8)
; > > >(sum #<procedure:cube> 6 #<procedure:inc> 8)
; > > > (sum #<procedure:cube> 7 #<procedure:inc> 8)
; > > > >(sum #<procedure:cube> 8 #<procedure:inc> 8)
; > > > > (sum #<procedure:cube> 9 #<procedure:inc> 8)
; < < < < 0
; < < < <512
; < < < 855
; < < <1071
; < < 1196
; < <1260
; < 1287
; <1295
; 1295
; >(sum2 #<procedure:cube> 2 #<procedure:inc> 8)
; <1295
; 1295
;
```



1.31 a. 写一个类似 sum 的过程，命名为 product。过程返回指定范围内 function 的 values 的乘积（product）。展示如何通过 product 定义 factorial。

```lisp
(define (product term a next b)
  (if (> a b)
      1
      (* (term a)
         (product term (next a) next b))))

(define (identity x) x)

(define (inc x)
  (+ x 1))

(define (factorial n)
  (product identity 1 inc n))

(factorial 3)
; >(product #<procedure:identity> 1 #<procedure:inc> 3)
; > (product #<procedure:identity> 2 #<procedure:inc> 3)
; > >(product #<procedure:identity> 3 #<procedure:inc> 3)
; > > (product #<procedure:identity> 4 #<procedure:inc> 3)
; < < 1
; < <3
; < 6
; <6
; 6
```

利用下面的公式，使用 product 计算 $\pi$ 。


$$
\frac {\pi} 4 = \frac {2\cdot4\cdot4\cdot6\cdot6\cdot8\cdots} {3\cdot3\cdot5\cdot5\cdot7\cdot7\cdots}
$$

$$
{\pi} = \int_1^nf= 4 \cdot \prod_{n= 1}^{\infty} f(n)
$$

$$
f(n)=\cases{
  \frac {n+1}{n+2}&if n is odd\cr
  \frac {n+2}{n+1}&if n is even
}
$$

```lisp
(define (product term a next b)
  (if (> a b)
      1
      (* (term a)
         (product term (next a) next b))))

(define (inc x)
  (+ x 1))

; 计算 pi
(define (pi-term n)
  (if (even? n)
      (/ (+ n 2) (+ n 1))
      (/ (+ n 1) (+ n 2))))

(define pi  
  (* 4 (product pi-term 1.0 inc 100)))

pi
; 3.141594224382854 
```



b. 如果你的过程 product 是线性递归，写一个线性迭代的版本。如果是线性迭代，写一个线性递归的版本。

```lisp
; 迭代版本
(define (product term a next b)
  (define (iter a result)
    (if (> a b)
      result
      (iter (next a) (* result (term a)))))
    (iter a 1))

(define (identity x) x)

(define (inc x)
  (+ x 1))

(define (factorial n)
  (product identity 1 inc n))

(require racket/trace)
(trace product)

(factorial 3)

; >(product2 #<procedure:identity> 1 #<procedure:inc> 3)
; <6
; 6
```



1.32 a. 过程 sum 和 product，可以看做 accumulate 的特殊例子。accumulate 通过 accumulation function 将 term 的连接到一起。

`(accumulate combiner null-value term a next b)` 

accumulate 和 sum、product 有着一样的 term 和 指定范围（a next b）。过程 combiner ，指定了当前的 term 和前一个 term 是以什么样的方式连接的。null-value 指定了当 terms 没有时的 base value 。

编写过程 accumulate，并展示 sum 和 product 如何通过 accumulate 定义。

```lisp
(define (accumulate combiner null-value term a next b)
  (if (> a b)
      null-value
      (combiner (term a) 
                (accumulate combiner null-value term (next a) next b))))

(define (inc n) (+ n 1))

(define (identity x) x)

(define (cube n)
  (* n n n))

; sum cube
(define (sum-cubes a b)
  (accumulate + 0 cube 1 inc 5))

(sum-cubes 1 5)

; product factorial
(define (factorial n)
  (accumulate * 1 identity 1 inc n))

(factorial 3)
```



b. 如果你的过程 accumulate 是线性递归，写一个线性迭代的版本。如果是线性迭代，写一个线性递归的版本。

```lisp
; 迭代版本
(define (accumulate2 combiner null-value term a next b)
  (define (iter a result)
    (if (> a b)
      result
      (iter (next a) (combiner (term a) result))))
  (iter a null-value))

(define (factorial2 n)
  (accumulate2 * 1 identity 1 inc n))

(trace accumulate2)
(factorial2 3)
```



1.33 通过引入 filter 概念，可以得到比 1.32 更加通用的 accumulate 版本。即只 combine 指定范围内，符合指定条件的 term。 filter-accumulate 有和 accumulate 一样的参数，以及一个描述 filter 的参数。

编写 filter-accumulate ，展示如何使用 filter-accumulate 实现下面的描述：

a. 求 [a ，b] 范围内素数的平方和（假设过程 prime？已给出）。

b. 小于 n 的所有与 n 互素（即满足 $GCD(i，n) = 1 ， i < n $ ）的正整数的乘积

```lisp
(define (filter-accumulate2 filter combiner null-value term a next b)
  (define (iter a result)
    (cond ((> a b) result)
          ((filter a) (iter (next a) (combiner (term a) result)))
          (else (iter (next a) result))))
  (iter a null-value))
```

a：

```lisp
; 求素数平方和
; filter prime?
; combiner +
; null-value 0
; term square
; next inc

(define (square x)
  (* x x))

(define (inc x)
  (+ x 1))

(define (expmod base exp m)
  (cond ((= exp 0) 1)
        ((even? exp) (remainder (square (expmod base (/ exp 2) m)) m))
        (else (remainder  (* base (expmod base (- exp 1) m)) m))))

(define (fermat-test n)
  (define (try-it a)
    (= (expmod a (- n 1) n) (remainder 1 n)))
  (try-it (+ 1 (random (- n 1)))))

(define (fast-prime? n times)
  (cond ((= times 0) true)
        ((fermat-test n) (fast-prime? n (- times 1)))
        (else false)))

(define (prime? n)
  (fast-prime? n 100))

(define (sum-prime a b)
  (filter-accumulate prime? + 0 square a inc b))

(sum-prime 2 3)
```

b：

```lisp
; 求小于 n 且与 n 互素的数的乘积
; filter x < n, GCD(x, n) = 1
; combiner *
; null-value 1
; term indentity
; next inc

(define (identity x) x)

(define (rmd a b)
  (remainder a b))

(define (gcd a b)
  (if (= b 0)
      a
      (gcd b (rmd a b))))

(define (sum-product a b)
  (define (relatively-prime? x)
    (= (gcd x b) 1))
  
  (filter-accumulate relatively-prime? * 1 identity a inc b))

(sum-product 2 5)
```





