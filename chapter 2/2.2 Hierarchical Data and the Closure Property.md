## 2.2 Hierarchical Data and the Closure Property

 

In general, an operation for combining data objects satisfies the **closure property** if the results of combining things with that operation can themselves be combined using the same operation.

Closure is the key to power in any means of combination beacuse it permits us to create hierarchical structures — structures made up of parts, which themselves are made up of parts,  and so on.

In this section, we take up the consequences of closure for compound data. We describe some conventional techniques for using **pairs** to represent **sequences** and **trees**, and we exhibit a graphics language that illustrates closure in a vivid way. 



###  2.2.1 Representing Sequences



`(list <a1> <a2> ... <an>)`

is equivalent to

`(cons <a1 (cons <a2> (cons...(cons <an> nill)...)))`



**List operations**

list-ref

```lisp
(define (list-ref items n)
  (if (= n 0)
      (car items)
      (list-ref (cdr items) (- n 1))))
```



length

```lisp
(define (length items)
  (if (null? items)
      0
      (+ 1 (length (cdr items)))))
```



length iteration

```lisp
(define (length items)
  (define (length-iter a count)
    (if (null? a)
        count
        (length-iter (cdr a) (+ count 1))))
  (length-iter items 0))
```



append

```lisp
(define (append list1 list2)
  (if (null? list1)
      list2
      (cons (car list1) (append (cdr list1) list2))))
```



[Python list 实现](https://github.com/python/cpython/blob/c30098c8c6014f3340a369a31df9c74bdbacc269/Include/listobject.h) 



### 练习

 2.17  last-pair，返回包含最后一个元素的 list

```lisp
(define (last-pair lst)
  (if (null? (cdr lst))
      (car lst)
      (last-pair (cdr lst))))
```



2.18 reverse

```lisp
(define (reverse lst)
  (define (reverse-iter lst-iter result)
    (if (null? lst-iter)
        result
        (reverse-iter (cdr lst-iter) (cons (car lst-iter) result))))
  (reverse-iter lst null))
```



2.19 重写 procedure cc，将第二个参数的类型改为 list 来指定可供使用的货币种类。

如：

```lisp
(define us-coins (list 50 25 10 5 1))
(define uk-coins (list 100 50 20 10 5 2 1 0.5))

; 调用 cc
(cc 100 us-coins)
; 292
```

这样做需要改变第二个参数的访问方式

```lisp
(define (cc amount coin-values)
  (cond 
    ((= amount 0) 1)
    ((or (< amount 0) (no-more? coin-values)) 0)
    (else
     (+ (cc amount (except-first-denomination coin-values))
        (cc (- amount (first-denomination coin-values)) coin-values)))))
```

使用基本运算和 list 定义 procedure first-denomination，except-first-denomination 和 no-more?。list coin-values 的顺序是否会影响 cc 的结果，为什么？

```lisp
(define (first-denomination values)
  (car values))

(define (except-first-denomination values)
  (cdr values))

(define (no-more? values)
  (null? values))

(define (cc amount coin-values)
  (cond 
    ((= amount 0) 1)
    ((or (< amount 0) (no-more? coin-values)) 0)
    (else
     (+ (cc amount (except-first-denomination coin-values))
        (cc (- amount (first-denomination coin-values)) coin-values)))))

(define us-coins (list 50 25 10 5 1))
(define us-coins-reverse (list 1 5 10 25 50))

(cc 100 us-coins)
(cc 100 us-coins-reverse)

; 292
; 292
```



2.20 **dotted-tail notation** 

```lisp
(define (f x y . z) <body>)
(f 1 2 3 4 5 6)
; x 1
; y 2 
; z (3 4 5 6)

(define (g . w) <body>)
(g 1 2 3 4 5 6)
; w (1 2 3 4 5 6)
```

使用 dotted-tail notaion 编写 same-parity，根据列表的第一个元素的奇偶，返回里面的所有偶数或奇数。

```lisp
(define (same-parity . items)
  (let ((even-odd (even? (car items))))
    (define (get-parity values)
      (cond ((null? values) null)
            ((not (xor even-odd (even? (car values))))
             (cons (car values) (get-parity (cdr values))))
            (else (get-parity (cdr values))))) 
    (get-parity items)))

(same-parity 1 2 3 4 5)
(same-parity 2 3 4 5)

; '(1 3 5)
; '(2 4)

```



#### Mapping over lists

```lisp
; transformation each element of the list
(define (scale-list items factor)
  (if (null? items)
      null
      (cons (* (car items) factor)
            (scale-list (cdr items) factor))))
(scale-list (list 1 2 3 4 5) 10)
; (10 20 30 40 50)

; abstract, a common pattern expressed as a higher-order procedure -- Map
(define (map proc items)
  (if (null? items)
      null
      (cons (proc (car items))
            (map proc (cdr items)))))

(map abs (list -10 2.5 -11.6 17))
; (10 2.5 11.6 17)

(map (lambda (x) (* x x))
     (list 1 2 3 4))
; (1 4 9 16)
```



2.21

```lisp
(define (square x)
  (* x x))

(define (square-list items)
  (if (null? items)
      null
      (cons (square (car items))
            (square-list (cdr items)))))

(define (square-list-map items)
  (map (lambda (x) (square x)) items))

(define test (list 1 2 3 4 5 6))

(square-list test)
(square-list-map test)
```



2.22

```lisp
(define (square-list items)
  (define (iter things answwer)
    (if (null? things)
        answer
        (iter (cdr things) 
              (cons (square (car things)) answer))))
  (iter items nil))
```

(cons (square (car things)) answer)))) 是一个压栈的动作。迭代时将 list 的数据从左至右取出来，然后cons 将数据从右向左放入新的 list 中，所以得到反序的 list。

```lisp
(define (square-list items)
  (define (iter things answwer)
    (if (null? things)
        answer
        (iter (cdr things) 
              (cons answer
                    (square (car things))))))
  (iter items nil))
```

这里只改变了 answer 在 cons 中的左右位置。若将 square-list 看成二叉树，不管 answer 在左还是右，都是在树的同一层级上，其深度不变，即与 list 的顺序无关。



2.23

```lisp
(define (for-each proc items)
  (if (null? items)
      (newline)
      (begin
        (proc (car items))
        (for-each proc (cdr items)))))

(for-each (lambda (x) (newline) (display x))
          (list 57 321 88))
```

