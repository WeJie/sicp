### 2.1.3 What Is Meant by Data?



In general, we can think of data as defined by some collection of selectors and constructors, together with specified conditions that these procedures must fulfill in order to be a valid repressentation. 



```lisp
(define (cons x y)
  (define (dispatch m)
    (cond ((= m 0) x)
      ((= m 1) y)
      (else (error "Argument not 0 or 1 -- CONS" m))))
  dispatch)

(define (car z) (z 0))

(define (cdr z) (z 1))
```



This example demonstrates that the ability to manipulate procedures as objectse auto matically provides the ability to represent compound data. 



Procedural representations of data will play a central role in our programming repertoire. This style of programming is often called message passing. 



### Exercise 

2.4  下面是 pairs 的另一种表达方式。验证 `(car (cons x y))` 对于任一 x 和 y ，都会得到 x。

```lisp
(define (cons x y)
  (lambda (m) (m x y)))

(define (car z)
  (z (lambda (p q) p)))

```



```lisp
; 对于任意 x，y
(car (cons x y))

; order of application
(car (lambda (m) (m x y)))

((lambda (m) (m x y)) (lambda (p q) p))

(lambda (x y) x)

x
```



与此对应的 cdr 定义是什么

```lisp
(define (cdr z)
  (z (lambda (p q) q)))
```



2.5 Represent pairs of nonnegative integers using only numbers and arithmetic operations. If we represent the pair a and b as the integer that is the product $2^a$ $3^b$. Give the corresponding definitions of the procedures cons, car, and cdr.

```lisp
(require racket/trace)

(define (n-cons a b)
  (* (expt 2 a) (expt 3 b)))

(define (n-car x)
  (define (iter x expt-a)
    (if (= (remainder x 2) 0)
        (iter (/ x 2) (+ expt-a 1))
        expt-a))
  (iter x 0))
        
(define (n-cdr x)
  (define (iter x expt-a)
    (if (= (remainder x 3) 0)
        (iter (/ x 3) (+ expt-a 1))
        expt-a))
  (iter x 0))

(n-cons 2 3)

(n-car (n-cons 2 3))

(n-cdr (n-cons 2 3))

; 108
; 2
; 3 
```



2.6 In a language that can manipulate procedures. we can get by without numbers(at least insofar as nonnegative integers are concerned) by implementing 0 and the operation of adding 1 as

```lisp
(define zero (lambda (f) (lambda (x) x)))

(define (add-1 n)
  (lambda (f) (lambda (x) (f ((n f) x)))))

```

This representation is known as [Church numerals](https://www.wikiwand.com/en/Church_encoding#/Church_numerals). Its inventor, [Alonzo Church](https://www.wikiwand.com/en/Alonzo_Church), the logician who invented the $\lambda$ calculus. 

Define one and two directly (not in terms of zero and add-1 ). (Hint: Use substitution to evaluate (add-1 zero)). Give a direct definition of the addition procedure + (not in terms of repeated applications of add-1)

```lisp
(define one
  (lambda (f) (lambda (x) (f x))))

(define two
  (lambda (f) (lambda (x) (f (f x)))))
  
```

The addition fuction plus(m, n) = m + n uses the identity $f^{x+n}(x) = f^m(f^n(x))$ .
$$
plus \equiv \lambda m\ldotp \lambda n \ldotp \lambda f \ldotp \lambda x \ldotp m \ f \  (n \ f \ x)
$$

```lisp
(define +
  (lambda (m) 
          (lambda (n) 
                  (lambda (f) 
                          (lambda (x) 
                                  ((m f) ((n f) x)))))))

```

