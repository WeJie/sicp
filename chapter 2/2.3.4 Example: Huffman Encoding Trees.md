### 2.3.4 Example: Huffman Encoding Trees

outline

[TOC]



- fixed-length code
- variable-length code
  - special separator code
  - prefix code, one particular scheme — Huffman encoding method



> Huffman code https://www.wikiwand.com/en/Huffman_coding



#### Generating Huffman trees

Given an "alphabet" of symbols and their relative frequencies, how do we constrcut the "best" code? (In other words, which tree will encode messages with the fewest bits?)



1. Begin with the set of leaf nodes, containing symbols and their frequencies, as determined by the initial data from which the code is to be constructed.
2. Now find two leaves with the lowest weights and merge them to produce a node that has these two nodes as its left and right branches.
3. The weight of the new node is the sum of the two weights.
4. Remove the two leaves from the original set and replace them by the new node.
5. Continue 1~4 step, merge two nodes with the smallest weights.
6. Stop when there is only one node left, which is the root of the entire tree.



#### Representing Huffman trees



```lisp
; leaf
(define (make-leaf symbol weight)
  (list 'leaf symbol weight))

; leaf selector
(define (leaf? object)
  (eq? (car object) 'leaf))
(define (symbol-leaf x) (cadr x))
(define (weight-leaf x) (caddr x))

; tree
(define (make-code-tree left right)
  (list left 
        right
        (append (symbols left) (symbols right))
        (+ (weight left) (weight right))))

; tree selectors
(define (left-branch tree) (car tree))
(define (right-branch tree) (cadr tree))
(define (symbols tree)
  (if (leaf? tree)
      (list (symbol-leaf tree))
      (caddr tree)))
(define (weight tree)
  (if (leaf? tree)
      (weight-leaf tree)
      (cadddr tree)))

```



#### The decoding procedure



```lisp
(define (decode bits tree)
  (define (decode-1 bits current-branch)
    (if (null? bits)
        '()
        (let ((next-branch
               (choose-branch (car bits) current-branch)))
             (if (leaf? next-branch)
                 (cons (symbol-leaf next-branch)
                       (decode-1 (cdr bits) tree))
                 (decode-1 (cdr bits) next-branch)))))
  (decode-1 bits tree))

(define (choose-branch bit branch)
  (cond ((= bit 0) (left-branch branch))
    ((= bit 1) (right-branch branch))
    (else (error "bad bit -- CHOOSE-BRANCH" bit))))
```



#### Sets of weighted elements



```lisp
(define (adjoin-set x set)
  (cond ((null? set) (list x))
    ((< (weight x) (weight (car set))) (cons x set))
    (else (cons (car set)
                (adjoin-set x (cdr set))))))

; symbol-frequency pairs
; ((A 4) (B 2) (C 1) (D 1))
(define (make-leaf-set pairs)
  (if (null? pairs)
      '()
      (let ((pair (car pairs)))
           (adjoin-set (make-leaf (car pair) ;symbol
                                  (cadr pair)) ; frequency
                       (make-leaf-set (cdr pairs))))))

```



##### Exercise 2.67

```lisp
(define sample-tree
  (make-code-tree (make-leaf 'A 4)
                  (make-code-tree
                   (make-leaf 'B 2)
                   (make-code-tree (make-leaf 'D 1)
                                   (make-leaf 'C 1)))))
(define sample-message '(0 1 1 0 0 1 0 1 0 1 1 1 0))

; '(A D A B B C A)
```



##### Exercise 2.68 

encode Huffman tree

```lisp
(define (encode message tree)
  (if (null? message)
      '()
      (append (encode-symbol (car message) tree)
              (encode (cdr message) tree))))

(define (encode-symbol symbol tree)
  (cond ((null? tree) '())
        ((leaf? tree) '())
        (else
         (let ((left-tree (left-branch tree))
               (right-tree (right-branch tree)))
           (cond ((element-of-set? symbol (symbols left-tree))
                  (cons 0 (encode-symbol symbol left-tree)))
                 ((element-of-set? symbol (symbols right-tree))
                  (cons 1 (encode-symbol symbol right-tree)))
                 (else (error "Symbol not in the tree" symbol)))))))
```



##### Exercise 2.69

Generates an Huffman encoding tree with symbol-frequency pairs

```lisp
; symbol-frequency pairs
; ((A 4) (B 2) (C 1) (D 1))
(define (generate-huffman-tree pairs)
  (successive-merge (make-leaf-set pairs)))

(define (successive-merge sets)
  (if (= 1 (length sets))
      (car sets)
      (successive-merge
       (adjoin-set
        (make-code-tree (car sets) (cadr sets)) (cddr sets)))))
```



##### Exercise 2.70

```lisp
(define rock-songs '((na 16) (yip 9) (Sha 3) (a 2) (Get 2) (job 2) (Wah 1) (boom 1)))

(define rock-songs-tree (generate-huffman-tree rock-songs))

(define message '(Get a job
                    Sha na na na na na na na na
                    Get a job
                    Sha na na na na na na na na
                    Wah yip yip yip yip yip yip yip yip yip
                    Sha boom
                    ))
(define encode-bits (encode message rock-songs-tree))
encode-bits
(length encode-bits)

;'(1 1 1 1 1 1 1 0 0 1 1 1 1 0 1 1 1 0 0 0 0 0 0 0 0 0 1 1 1 1 1 1 1 0 0 1 1 1 1 0 1 1 1 0 0 0 0 0 0 0 0 0 1 1 0 1 1 1 0 1 0 1 0 1 0 1 0 1 0 1 0 1 0 1 0 1 1 1 0 1 1 0 1 0)

; 84
```

如果使用 fixed-length code，8 个符号，每个符号需要 $log 2^3$ 即 3 位的 bit 来表示。整个 message 一共有 36 个符号，总共需要 108 bit 来表示。