### 1.2.4 Exponentiation 求冥

$$
b^n = \cases {
1&if n = 0  \cr
b \cdot b^{n-1} &if n > 0
}
$$

```lisp
(define (expt b n)
  (if (= n 0)
      1
      (* b (expt b (- n 1)))))


(define (expt2 b n)
  (expt-iter b 1 n))

(define (expt-iter b product counter)
  (if (= counter 0)
      proudct
      (expt-iter (* b product) (- counter 1))))
```

线性递归，时间复杂度和空间复杂度都是 $\theta(n)$

线性迭代，时间复杂度是$\theta(n)$，空间复杂度是 $\theta(1)$

计算步骤基于 n 线性增长。



通过连续求平方(如，$2^8 = 2^4\cdot2^4 =2^2\cdot2^2\cdot2^2\cdot2^2 =256$)，减少乘幂的步骤：
$$
b^n = \cases{
  1 & if n=0 \cr
  (b^{n/2})^2 & if n>0，n 是偶数 EVEN \cr
  b \cdot b^{n-1} & if n>0，n 是奇数 ODD
}
$$

```lisp
(define (square n)
  (* n n))

(define (fast-expt b n)
  (cond ((= n 0) 1)
        ((even? n) (square (fast-expt b (/ n 2)))); n 是偶数时，计算步骤 = 以 2 为底 n 的对数
        (else (* b (fast-expt b (- n 1))))))
; 这还是一个递归算法
; 要改为迭代算法需要引入状态变量，如 1.16
```



1.16  设计一个连续求平方的求冥的迭代过程，计算步骤对数增长，类似 fast-expt。（提示：观察 $(b^{n/2})^2 = (b^2)^{n/2}$ （递归转迭代，基于$b^2$迭代求冥），保持指数 n 和基数 b，另外加上状态变量 a，定义状态转换规则，$(product，a，b^n)$ (没看出跟题目有什么关系) ，在状态到状态的过程中保持不变。进程开始时 a 为 1，在进程结束时返回 a 作为结果。一般来说，定义一个在状态到状态的过程中保持不变的不变量 — 状态转换规则，是一种设计迭代算法的有效方式。）
$$
b^n = \cases{
  a & if n=0 \cr
  (b^2)^{n/2} & if n>0，n 是偶数 EVEN \cr
  b\cdot (b^2)^{\frac {n-1}2} & if n>0，n 是奇数 ODD
}
$$

```lisp
(define (square n)
  (* n n))

(define (fast-expt b n)
  (expt-iter b n 1))

; n 为偶数时，product = a 保持不变
; n 为奇数时，n - 1, 继续计算 expt-iter(回到 n 为偶数的情况)，多出来的 b 放入 product，product = a * b
(define (expt-iter b n a)
  (cond ((= n 0) a)
        ((even? n) (expt-iter (square b) (/ n 2) a))
        ((odd? n) (expt-iter b (- n 1) (* b a)))))

(require racket/trace)
(trace expt-iter)
(fast-expt 2 4)

; >(expt-iter 2 4 1)
; >(expt-iter 4 2 1)
; >(expt-iter 16 1 1)
; >(expt-iter 16 0 16)
; <16
; 16
```



1.17 这一节中求冥算法是基于重复的乘法运算。类似，整数的乘法运算可以看作基于重复的加法运算。
$$
a\cdot b = \cases{
 0 & if b=0\cr
  a+ a\cdot (b-1) & if b > 0
}
$$

```lisp
(define (multi a b)
  (if (= b 0)
      0
      (+ a (multi a (- b 1)))))
```

这个算法的复杂度是基于 b 线性增长的。

现在假定 double 运算求整数的双倍、Halve 求偶数的一半，设计乘法运算类似 fast-expt 的对数算法。

$a\cdot b = [a\cdot (b/2)]\cdot 2$ ，递归：
$$
a\cdot b = \cases{
  0 & if b=0\cr
  [a\cdot(b/2)]\cdot2 & if b>0，b 是偶数 EVEN \cr
  a+ a\cdot (b-1)& if b>0，b 是奇数 ODD
}
$$
$a\cdot b = [a\cdot (b/2)]\cdot 2= (a\cdot2)\cdot (b/2) $，迭代：
$$
a\cdot b = \cases{
  0 & if b=0\cr
  (a\cdot2)\cdot (b/2) & if b>0，b 是偶数 EVEN \cr
  a+ (a\cdot2)\cdot (\frac {b-1}2) & if b>0，b 是奇数 ODD
}
$$

```lisp
(define (double n)
  (+ n n))

(define (hive n)
  (/ n 2))

; 递归版本
(define (fast-multi a b)
  (cond ((= b 0) 0)
        ((even? b) (double (fast-multi a (hive b))))
        ((odd? b) (+ a (fast-multi a (- b 1))))))

; 迭代版本
(define (fast-multi-iter a b)
  (multi-iter a b 0))

(define (multi-iter primary counter product)
  (cond ((= counter 0) product)
        ((even? counter) (multi-iter (double primary) (hive counter) product))
        ((odd? counter) (multi-iter primary (- counter 1) (+ product primary)))))

```



1.18 根据 1.16、1.17 的技术，设计一个基于 adding、doubling、halving 的 迭代 procedure 计算两个数的乘积，要求计算步骤呈对数增长（1.17 迭代版本）。

```lisp
(define (double n)
  (+ n n))

(define (hive n)
  (/ n 2))

(define (fast-multi-iter a b)
  (multi-iter a b 0))

(define (multi-iter primary counter product)
  (cond ((= counter 0) product)
        ((even? counter) (multi-iter (double primary) (hive counter) product))
        ((odd? counter) (multi-iter primary (- counter 1) (+ product primary)))))
```



1.19 Fibonacci 的 transformation
$$
T=\cases{
  a \leftarrow a + b \cr
  b \leftarrow a
}
$$
Fibonacci 由（1，0）开始，一次又一次应用 $T$（即$T^n$）得到 Fib（n+1）和Fib（n）。$T$ 是 $T_{pq}$ 当 p = 0 和 q = 1 时的特殊情况。
$$
T_{pq} = \cases{
  a \leftarrow bq + aq + ap \cr
  b \leftarrow bp + aq
} 
$$
 类似 fast-expt procedure，可以计算 $T^n$ 的连续求平方，$T_{pq}^2 = T_{p'q'}$ ，即可得到，时间复杂度是对数增长的 procedure。
$$
T_{p'q'}=T_{pq}^2 = \cases{
  a \leftarrow (bp+aq)q + (bq+aq+ap)(q + p) \cr
  b \leftarrow (bp+aq)p + (bq+aq+ap)q
}
\Rightarrow 
\cases{
    a \leftarrow b(2pq+q^2)+a(p^2+q^2)+a(2pq+q^2) \cr
  b \leftarrow b(p^2+q^2) + a(2pq+q^2)
}
$$
对比$T_{p'q'}=T_{pq}$，可以得到 $p' = p^2 + q^2$， $q' = 2pq + q^2$

```lisp
(define (square n)
  (* n n))

(define (fib n)
  (fib-iter 1 0 0 1 n))

(define (fib-iter a b p q count)
  (cond ((= count 0) b)
        ((even? count) (fib-iter a
                                 b
                                 (+ (square p) (square q));p'
                                 (+ (* 2 p q) (square q));q'
                                 (/ count 2)))
        (else (fib-iter (+ (* b q) (* a q) (* a p))
                        (+ (* b p) (* a q))
                        p
                        q
                        (- count 1)))))

(fib 5)
```



```
    [ 1 1 ] n      [ F(n+1) F(n)   ]
    [ 1 0 ]    =   [ F(n)   F(n-1) ]
```



>a、b、p、q 的关系是怎么建立的？
>
>[Design and Analysis of AlgorithmsLecture notes for January 9, 1996](http://www.ics.uci.edu/~eppstein/161/960109.html)
>
>[kaldewaij-programming.pdf](/material/kaldewaij-programming.pdf) 
>
>[Programming: the derivation of algorithms——Kaldewaij, Anne](https://book.douban.com/subject/3810298/) 
>
>http://sicp.csrg.org/papers/kaldewaij-programming.pdf
>
>http://sicp.csrg.org/
>
>[矩阵的本质就是线性方程式](http://www.ruanyifeng.com/blog/2015/09/matrix-multiplication.html)
>
>https://www.wikiwand.com/en/Matrix_multiplication
>
>https://www.wikiwand.com/en/Linear_map
>
>[蒙哥马利算法](https://www.wikiwand.com/zh-hans/%E8%92%99%E5%93%A5%E9%A9%AC%E5%88%A9%E7%AE%97%E6%B3%95)

