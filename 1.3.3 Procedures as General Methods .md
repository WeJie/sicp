### 1.3.3 Procedures as General Methods 过程作为一般性方法



我们在 1.1.4 节介绍了 compound procedure 作为数值操作的抽象模式，其依赖于特定数值的参与。在高阶过程中，类似 1.3.1 节中的过程 integral ，我们开始了解一个更加强大的抽象：procedure 用于表达计算的一般性方法，其依赖于某个特定 function 参与。在这一节，我们将讨论两个更加复杂的例子——finding zeros 和 fixed points of functions 的一般性方法——并展示这些方法如何通过 procedure 直接表达。 



#### Finding roots of equations by the [half-interval method](https://www.wikiwand.com/en/Bisection_method)

half-interval 方法是一个简单又有效地查找方程$f(x) = 0$的根的技术，这里 $f$ 是一个连续的函数。其思想是，对于给出的点 a、b 满足 $ f(a) < 0 < f(b) $ ，则 $f$ 必定有至少一个 0 根，位于 a、b 之间。为了找到 0，设 x 为 a、b 的平均数，计算 $f(x)$ 。如果 $f(x) > 0$ ，则 $f$ 有一个 0 根在 a 和 x 之间。如果 $f(x) < 0$ ，则 $f$ 有一个 0 根在 x 和 b 之间。经过这样反复计算， 我们能得到一个越来越小的区间，$f$ 的 0 根就在其中。当区间足够小时，停止计算。

过程每执行一次，区间的不确定性减少一半，其步骤的增长为 $\theta(\log(L/T))$ ，L 是初始区间的长度，T 是容错度（即当区间多大时，为「足够小」）。

以下是实现该策略的 procedure：

```lisp
(define (search f neg-point pos-point)
  (let ((midpoint (average neg-point pos-point)))
    (if (close-enough? neg-point pos-point)
        midpoint
        (let ((test-value (f midpoint)))
          (cond ((positive? test-value) (search f neg-point midpoint))
                ((negative? test-value) (search f midpoint pos-point))
                (else midpoint))))))
```

我们假设最初给了函数 $f$ 和一个正数点，一个负数点。首先计算两点的中点，检查区间是否足够小了，如果是就返回 midpoint 作为答案。否则，计算 $f$ 的 modpoint 的值。如果是正数，计算负数点到 midpoint 的区间。如果是负数，计算 midpoint 到正数点的区间。如果得到 0，midpoint 就是我们要找的 0 根。

我们使用 1.1.7 节计算平方根中用到的方法来测试 endpoint 是否 「close enough」。

```lisp
(define (close-enough? x y)
  (< (abs (- x y)) 0.001))
```



因为不知道给予的两个点，对应 $f$ 的值的正负号， 所以过程 Search 还不能直接用上。我们使用下面的过程来处理这个问题。

```lisp
(define (half-interval-method f a b)
  (let ((a-value (f a))
        (b-value (f b)))
    (cond ((and (negative? a-value) (positive? b-value)) (search f a b))
          ((and (negative? b-value) (positive? a-value)) (search f b a))
          (else (error "Values are not of opposite sign" a b)))))
```

求 $\sin x = 0,x\in(2,4)$ ，即 $\pi$

```lisp
(half-interval-method sin 2.0 4.0)
; 3.14111328125
```

求 $x^3 - 2x - 3 = 0, x \in (1,2)$ 

```lisp
(half-interval-method (lambda (x) (- (* x x x) (* 2 x) 3)) 1.0 2.0)
; 1.89306640625
```



#### Finding [fixed points](https://www.wikiwand.com/en/Fixed_point_(mathematics)) of functions

fixed point 满足 $f(x) = x$ 。对于一些函数可以从初始猜测开始，反复应用 $f$ ，$f(x), f(f(x)), f(f(f(x)))$  直到得到的数值变化越来越小。

```lisp
(define tolerance 0.00001)

(define (fixed-point f first-guess)
  (define (close-enough? v1 v2)
    (< (abs (- v1 v2)) tolerance))
  (define (try guess)
    (let ((next (f guess)))
      (if (close-enough? guess next)
          next
          (try next))))
  (try first-guess))


(fixed-point cos 1.0)
; 0.7390822985224023
```

求 $y = \sin y + \cos y$ 的 fixed point

```lisp
(fixed-point (lambda (y) (+ (sin y) (cos y))) 1.0)
; 1.2587315962971173
```



1.1.7 节的查找平方根也可以看做查找 $y = x/y$ 的 fixed point。

```lisp
(define (sqrt x)
  (fixed-point (lambda (y) (/ x y)) 1.0))
```

但是这个过程的计算会不断循环，并不会收敛。

一个控制这种震荡的方法是，防止猜测的值大幅变化。可以从计算 $ y = x/y$ 变为计算 $y=(1/2)(y + x/y)$ 

```lisp
(define (sqrt x)
  (fixed-point (lambda (y) (average y (/ x y))) 1.0))
```

这种逐步平均逼近求解的方法，称为 average damping。



### 练习

1.35 黄金比例 $\phi$ 是 $x \rightarrow 1+ 1/x$ 的 fixed point。使用过程 fixed-point 计算 $\phi$。

```lisp
(define tolerance 0.00001)

(define (fixed-point f first-guess)
  (define (close-enough? v1 v2)
    (< (abs (- v1 v2)) tolerance))
  (define (try guess)
    (let ((next (f guess)))
      (if (close-enough? guess next)
          next
          (try next))))
  (try first-guess))

(fixed-point (lambda (x) (+ 1 (/ 1 x))) 1.0)
```



1.36 修改过程 fixed-point ，使用 newline 和 display 打印出求 fixed point 的过程。通过找到 $x \rightarrow \log(1000)/\log(x)$ 的 fixed point，求解 $x^x = 1000$ 。比较使用 average damping  与否所需的步骤的数量差异（注意，初始猜测值不能是 1，否则因为 $\log(1) = 0$ 会造成除数为 0）

```lisp
(require racket/trace)

(define tolerance 0.00001)

(define (fixed-point f first-guess)
  (define (close-enough? v1 v2)
    (< (abs (- v1 v2)) tolerance))
  (define (try guess)
    ;(display guess)
    ;(newline)
    (let ((next (f guess)))
      (if (close-enough? guess next)
          next
          (try next))))
  (trace try)
  (try first-guess))


(fixed-point (lambda (x) (/ (log 1000) (log x))) 5.0)

(define (average a b)
  (/ (+ a b) 2))

(fixed-point (lambda (x) (average x (/ (log 1000) (log x)))) 5.0)

; >(try 5.0)
; >(try 4.29202967422018)
; >(try 4.741863119908242)
; >(try 4.438204569837609)
; >(try 4.635299887107611)
; >(try 4.50397811613643)
; >(try 4.589989462723705)
; >(try 4.53301150767844)
; >(try 4.570475672855484)
; >(try 4.545720389670642)
; >(try 4.562024936588171)
; >(try 4.551263234080531)
; >(try 4.55835638768598)
; >(try 4.553676852183342)
; >(try 4.55676216434628)
; >(try 4.554727130670954)
; >(try 4.556069054770006)
; >(try 4.555184018843625)
; >(try 4.5557676565438205)
; >(try 4.555382746639082)
; >(try 4.55563658243586)
; >(try 4.555469180245326)
; >(try 4.555579577900997)
; >(try 4.5555067722873686)
; >(try 4.5555547860484085)
; >(try 4.555523121789556)
; >(try 4.555544003742869)
; >(try 4.555530232469306)
; <4.555539314360711
; 4.555539314360711
; 计算了 27 次

; >(try 5.0)
; >(try 4.64601483711009)
; >(try 4.571611286076025)
; >(try 4.558294317536066)
; >(try 4.556006022881116)
; >(try 4.555615799731297)
; >(try 4.555549342575593)
; >(try 4.555538027101999)
; <4.5555361005218895
; 4.5555361005218895
; 计算了 8 次
```



1.37 a. 一个[无穷连续分数](https://www.wikiwand.com/en/Continued_fraction)是这样的：
$$
f = \frac{N_1} {D_1 + \frac {N_2} {D_2 + \frac {N_3} {D_3 + ....}}}
$$
作为例子，这里展示了一个无穷连续分数随着 $N_i$ 和 $D_i$  expansion，逐渐逼近 $1/\phi$ ，$\phi$ 是黄金分割率。一个逼近无穷连续分数的方法是在给出一定数量的 items 后截断 expansion。截断后称为 k 项有限连续分数：
$$
f = \frac{N_1} {D_1 + \frac {N_2} {... + \frac {N_K} {D_K}}}
$$
假设 n 和 d 都是有一个参数（term 的索引 i）的过程，其返回值为连续分数的 $term_i$ 的 $N_i$ 和 $D_i$ 。定义一个过程 cont-frac 执行 `(cont-frac n d k)` 计算 k 项有限连续分数的值。使用下面的过程检查你的过程是否逼近 $1/\phi$ 。

```lisp
(cont-frac (lambda (i) 1.0)
           (lambda (i) 1.0)
           k)
```

如果要取得 4 位小数的精度，k 必须设置到多大？![\Phi ={1 \over \phi }={1 \over 1.61803\,39887\ldots }=0.6180339887\ldots ](https://wikimedia.org/api/rest_v1/media/math/render/svg/225cfa92522b8d5c6f2db1a940eff0c6fbae31fd)

```lisp
#lang racket
(require racket/trace)

(define (cont-frac n d k)
  (define (next i)
    (+ i 1))
  (define (frac i)
    (if (= i k)
        (/ (n i) (d i))
        (/ (n i) (+ (d i) (frac (next i))))))
  (trace frac)
  (frac 1))

(cont-frac (lambda (i) 1.0)
           (lambda (i) 1.0)
           11)

; 0.6180555555555556
```



b. 如果你的 cont-frac 过程是递归的，写一个迭代的版本。如果是迭代的，写一个递归的版本。
$$
cf(k) = n_k/d_k
$$

$$
cf(k-1) = n_{k-1}/(d_{k-1} + cf(k))
$$

$$
cf(k-2) = n_{k-2}/(d_{k-2} + cf(k-1))
$$



```lisp
; 迭代版本
(define (cont-frac-iter n d k)
  (define (next i)
    (- i 1))
  (define (iter i result)
    (if (= i 0)
        result
        (iter (next i)
              (/ (n i)
                 (+ (d i) result)))))
  ;(trace iter)
  (iter (next k) (/ (n k) (d k))))

(cont-frac-iter (lambda (i) 1.0)
                (lambda (i) 1.0)
                11)
```



1.38 瑞士数学家 Leonhard Euler 发表了一篇论文 [**De Fractionibus Continuis**](http://eulerarchive.maa.org/pages/E071.html) ，里面提到了 $e - 2$ 的连续分式的展开，e 是[自然对数](https://www.wikiwand.com/en/Natural_logarithm)。在这个分式中 $N_i$ 都是 1，$D_i$ 是连续序列 $1，2，1，1，4，1，1，6，1，1，8…$ 。基于 Euler 的理论，使用 1.37 中的过程 cont-frac 编写程序求 e 的逼近值。
$$
d(i) = \cases{
((i - 2)/3 + 1) \cdot  2 & if (i-2) mod 3 = 0 \cr
1 & otherwise
}
$$


```lisp
#lang racket
(require racket/trace)
(define (cont-frac n d k)
  (define (next i)
    (- i 1))
  (define (iter i result)
    (if (= i 0)
        result
        (iter (next i) (/ (n i) (+ (d i) result)))))
  (trace iter)
  (iter k 0))

(define (d i)
  (if (= 0 (remainder (- i 2) 3))
      (/ (+ (* i 2) 2) 3)
      1))

(define e
  (+ 2 (cont-frac (lambda (i) 1.0)
                  d
                  10)))
e
; 2.7182817182817183
```



1.39 1770 年德国数学家 J.H.Lambert 发表了正切函数的连分式：
$$
\tan x =  \frac{x} {1 - \frac {x^2} {3 - \frac {x^2} {5 - ....}}}
$$
这里 x 表示弧度。基于 Lambert 的公式，定义过程 （tan-cf x k）计算正切函数的逼近值。 K 指定 term 的计算次数，类似 1.37 。

```lisp
#lang racket

(require racket/trace)

(define (cont-frac n d k)
  (define (next i)
    (- i 1))
  (define (iter i result)
    (if (= i 0)
        result
        (iter (next i)
              (/ (n i)
                 (+ (d i) result)))))
  ;(trace iter)
  (iter (next k) (/ (n k) (d k))))

(define (tan-cf x k)
  (define (n i)
    (if (= i 1)
        x
        (- (* x x))))
  
  (define (d i)
     (- (* i 2) 1))
  (cont-frac n d k))

(tan-cf 10.0 100)
(tan 10)

; 0.6483608274590866
; 0.6483608274590867 
```

